buildscript {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:$openApiVersion"
        classpath "com.masabi.swagwire:swagwire-codegen:$swagwireVersion"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id "org.openapi.generator" version "4.2.3"
}

ext.GeneratorTask = org.openapitools.generator.gradle.plugin.tasks.GenerateTask
defaultTasks "clean", "buildPetStore", "buildPetStoreTest"

repositories {
    gradlePluginPortal()
    mavenLocal()
    maven { url 'https://artifactory.masabi.com/artifactory/libs-release' }
    maven { url 'https://artifactory.masabi.com/artifactory/libs-release-local' }
    maven { url 'https://artifactory.masabi.com/artifactory/libs-snapshot-local' }
}

dependencies {
    testImplementation(
        "org.spockframework:spock-core:$spockVersion",
    )

    // dependencies required to run swagwire
    // swagwire-core has compile time dependencies on the other libraries required
    testImplementation(
        "com.masabi.swagwire:swagwire-core:$swagwireVersion",
        "com.github.tomakehurst:wiremock:$wiremockVersion",
        "ch.qos.logback:logback-classic:$logbackVersion",
    )

    implementation "org.apache.groovy:groovy-all:$groovyVersion"

    // dependencies needed for production code generation - these would be part of your standard build
    implementation(
        "org.glassfish.jersey.core:jersey-client:$jerseyVersion",
        "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion",
        "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion",
        "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
        "org.openapitools:jackson-databind-nullable:$jacksonNullableVersion",
        "com.google.code.findbugs:jsr305:$findbugsVersion",
        "io.swagger.core.v3:swagger-annotations:$swaggerAnnotationsVersion"
    )

    testImplementation(
        "javax.xml.bind:jaxb-api:2.3.1"
    )
}

apply from: 'swagger-codegen.gradle'