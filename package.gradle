apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: '../version.properties'

group = "com.masabi.swagwire"

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def sonatypeCreds = [
    userName: project.properties['ossrhUsername'] ?: "",
    password: project.properties['ossrhPassword'] ?: ""
]
def mavenDeployRepo = [
    releases: project.properties['mavenReleaseRepo'] ?: "https://oss.sonatype.org/service/local/staging/deploy/maven2/",
    snapshot: project.properties['mavenSnapshotRepo'] ?: "https://oss.sonatype.org/content/repositories/snapshots/"
]

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Swagwire'
                description = 'Swagwire combines Swagger and Wiremock to generate tests stubs for HTTP integration testing'
                url = 'https://github.com/Masabi/swagwire'

                scm {
                    url = 'https://github.com/Masabi/swagwire'
                    connection = 'scm:git:git://github.com/Masabi/swagwire.git'
                    developerConnection = 'scm:git:git@github.com:Masabi/swagwire.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'tddmonkey'
                        name = 'Colin Vipurs'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username sonatypeCreds.userName
                password sonatypeCreds.password
            }
            url = version.endsWith('SNAPSHOT') ? mavenDeployRepo.snapshot : mavenDeployRepo.releases
        }
    }

}

signing {
    sign publishing.publications.mavenJava
}